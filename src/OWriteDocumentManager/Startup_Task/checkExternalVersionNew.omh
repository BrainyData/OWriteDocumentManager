Set reference iref to $components.[pLibName]
If iref
	;  get version from component library
	;  this will be in the format studio_version.owrite_version
	Calculate version as iref.$version
	Calculate externalVersionStudio as strtok(nam(version),'.')
	Calculate externalVersion as version
	
	If externalVersion>=pMinVersion     ;; our external component version is OK
		;  test the Studio version
		Calculate version as sys(1)
		Calculate studio_version as strtok(nam(version),'.')*10+strtok(nam(version),'.')
		Switch externalVersionStudio
			Case 43
				If (studio_version>=43)&(studio_version<50)
					Quit method kTrue
				End If
			Case 50
				If (studio_version>=50)&(studio_version<52)
					Quit method kTrue
				End If
			Case 52
				If (studio_version>=52)
					Quit method kTrue
				End If
			Case 60
				If (studio_version>=60)
					Quit method kTrue
				End If
			Case 61
				If (studio_version>=61)
					Quit method kTrue
				End If
			Case 80
				If (studio_version>=80)
					Quit method kTrue
				End If
			Case 81
				If (studio_version>=81)
					Quit method kTrue
				End If
		End Switch
		;  if we get here DLL is not compatible with this version of Studio
		Calculate pErrorText as con(pErrorText,"...the file ",pFileName," is not compatible with this version of Studio//")
		Quit method kFalse
	Else     ;; we are not ok
		Calculate pErrorText as con(pErrorText,"...",pFileName," version ",pMinVersion," required...//")
		Quit method kFalse
	End If
Else
	Calculate pErrorText as con(pErrorText,"...",pFileName," failed to load...//")
	Quit method kFalse
End If