If $ctask.tvDebug
	Send to trace log {[$cclass().$name].[$cmethod().$name]: pEventCode=[pEventCode]    pNextCode = [pNextCode]    $cobj().$name=[$cobj().$name]}
End If
On evBefore
	Switch $cobj.$objtype
		Case kEntry,kMultilineEntry,kCombo
			Calculate $cobj.$keyevents as kTrue
	End Switch
On evKey
	Switch $cobj.$objtype
		Case kEntry,kMultilineEntry,kCombo
			Set reference ref to $cobj()     ;; in case something happens to $cobj
			Process event and continue
			Do method contentChanged (ref) Returns changed
			If changed
				Calculate ref.$fieldstyle as 'CtrlEditText'
			End If
	End Switch
On evAfter
	Switch $cobj.$objtype
		Case kEntry,kMultilineEntry,kCombo
			If $cobj.$fieldstyle='CtrlEditTextDis'
				Calculate $cinst.[$cobj.$dataname] as #NULL
			End If
	End Switch
On evClick
	Switch $cobj.$objtype
		Case kCombo
			Set reference ref to $cobj()     ;; in case something happens to $cobj
			Process event and continue
			Do method contentChanged (ref) Returns changed
			If changed
				Calculate ref.$fieldstyle as 'CtrlEditText'
			End If
		Case kCheckbox
			If $cobj.$iconid=11004
				Calculate $cobj.$iconid as 0
			End If
		Case kRadio
			If $cobj.$iconid=11007
				Set reference ref to $cobj
				If ref.$container
					Set reference parentRef to ref.$container
				Else
					Set reference parentRef to $cinst
				End If
				While ref.$dataname=$cobj.$dataname
					Calculate ref.$iconid as ref.$userinfo     ;; CHANGE_V3
					Set reference ref to parentRef.$objs.$next(ref)
				End While
			End If
	End Switch