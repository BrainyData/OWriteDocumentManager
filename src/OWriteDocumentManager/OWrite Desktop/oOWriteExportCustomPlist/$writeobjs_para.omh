;  PURPOSE:     ;; export paragraph object
;  USAGE:     ;; called by ...$writeobjs
;  SUB-CLASSING:     ;; sub-class this method to export paragraph text and styles


;  export paragraph info ---

;  --- start Omnis TEXT buffer
Do method $cinst.$buffer_start Returns #F
If flag true
	
	;  --- open paragraph dictionary
	Text: {[ivIndent]<dict>} (Linefeed)
	Do method Indent (kTrue)
	
	;  --- is paragraph a simple page-break
	If pObjIsPageBreak
		
		;  --- write out only type and display text for page breaks
		Do method $cinst.$buffer_property ("ObjType","string",ObjTypePageBreakStr)
		Do method $cinst.$buffer_property ("ObjData","utf8",pObjData)
		
	Else
		
		;  --- export paragraph type and data
		Do method $cinst.$buffer_property ("ObjType","string",$cinst.$objtype_tostring(pObjType))
		Do method $cinst.$buffer_property ("ObjData","utf8",pObjData)
		
		;  --- export paragraph style info
		Do method $cinst.$writestyles (pObjStylesList) Returns #F
		If flag false
			Quit method kFalse
		End If
		
		;  --- export paragraph embedded/anchored objects
		Do method $cinst.$writeobjs (pObjChildList) Returns #F
		If flag false
			Quit method kFalse
		End If
		
		
	End If
	
	;  --- close paragraph dictionary
	Do method Indent (kFalse)
	Text: {[ivIndent]</dict>} (Linefeed)
	
	;  --- close Omnis text buffer     ;; if not nested, writes buffer to output
	Do method $cinst.$buffer_end Returns #F
	
End If

Quit method #F