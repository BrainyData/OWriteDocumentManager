;  this example will merge the document template with each client record
;  and produce a single PDF document that can be displayed on the client

Do $cdevice.$assign(kDevPdf)
;  we are printing PDF to memory and are asking that the output is send to $ctask.$pdfoutput()
Do $cdevice.$setparam(kDevPdfFileName,con("$iremotetasks.",$ctask().$name,".$pdfoutput(#)"))

;  open our PDF device to receive multiple prints
Do $cdevice.$open()

;  for each client record load, merge and print the document template
Begin reversible block
	Set main file {fClients}
End reversible block
Find first
While flag true
	Do $cinst.$loaddata(ivCallerRef.ivDocumentData,kWriFmtDefault,kFalse,kWriSaveAsList,kTrue,kWriSaveRawPicts,kTrue)
	Do $cinst.$evalcalcs.$assign(kTrue)
	Do $cinst.$print("",kFalse)
	Next
End While

;  close our device, this will send the binary data to our task's $pdfoutput
Do $cdevice.$close()

;  prepare an url that will fetch our PDF data for display on client
TCPGetMyAddr  Returns addr
Do $modes.$dotoolmethod(kEnvToolBrowser,160) Returns port     ;; Returnï½“ the port Omnis is listening on ($prefs.$serverport is unreliable as it can be zero in which case Omnis chooses a port)
Calculate addr as "127.0.0.1"
Calculate url as con("http://",addr,":",port,"/ultra?OmnisServer='",addr,":",port,"'&OmnisLibrary=example&OmnisClass=rtGetPDF&PDFOwner=",$ctask().$name)
;  show the url
;  Send to trace log {$mailmerge([url])}
Quit method url