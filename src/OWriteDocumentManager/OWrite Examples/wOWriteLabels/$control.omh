On evEnabled
	Calculate $cobj.$backgroundtheme as kBGThemeNone
On evDisabled
	Calculate $cobj.$backgroundtheme as kBGThemeParent
On evClick
	Calculate iInModTest as kTrue     ;; prevent notation errors, modTest may change $ctarget which will cause en evAfter
	Do method modTest ($cobj)
	Calculate iInModTest as kFalse
	Calculate $ctarget as $cobj
On evAfter
	If not(iInModTest)
		If pNextCode=evMouseDown
			;  grrr - only test pClickedField if next code is a mouse down (on another window field)
			;  otherwise we get nasty re-cursive notation errors in development mode
			;  If pClickedField().$name=ref_btnCancel().$name
			;  don't update if cancel button clicked
			;  Quit event handler (Pass to next handler)
			;  End If
		Else If pNextCode=evCloseBox
			;  don't update if close-box clicked
			Quit event handler (Pass to next handler)
		End If
		Calculate iInModTest as kTrue     ;; prevent notation errors, modTest may change $ctarget which will cause en evAfter
		Do method modTest ($cobj)
		Calculate iInModTest as kFalse
	End If
On evKey
	Switch $cobj().$name
		Case 'Preview','PickList'     ;; redirect key events for Preview to PickList
			If pSystemKey=kUp
				If refPickListData.$line>1
					Calculate refPickListData.$line as refPickListData.$line-1
					Do refPickList.$redraw()
					Do refPickList.$rowChanged
				End If
				Quit event handler (Discard event)
			Else If pSystemKey=kDown
				If refPickListData.$line<refPickListData.$linecount
					Calculate refPickListData.$line as refPickListData.$line+1
					Do refPickList.$redraw()
					Do refPickList.$rowChanged
				End If
				Quit event handler (Discard event)
			End If
			If pSystemKey=kTabKey     ;; Preview and PickList appear to swallow tab keys
				If #SHIFT
					Calculate $ctarget as $cinst.$objs.cFontSizeList
				Else
					Calculate $ctarget as $cinst.$objs.ContentMargin
				End If
			End If
	End Switch