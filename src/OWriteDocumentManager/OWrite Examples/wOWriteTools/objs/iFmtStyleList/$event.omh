On evClick     ;; CHANGE_V3
	Do $cinst.$setFocusOWrite()
	Switch pRow.C2
		Case kStyleActionSetStyleName
			Do iFRefOWrite.$startundo()
			Calculate iFRefOWrite.$curstylename as pRow.C1
			Do iFRefOWrite.$endundo("Change Style")
		Case kStyleActionReformatParagraph
			No/Yes message Reformat Paragraph {Reformat the current paragraph with the style "[iFRefOWrite.$curstylename]"? This action cannot be undone.}
			If flag true
				Do iFRefOWrite.$undoenabled.$assign(kFalse)
				Calculate iFRefOWrite.$curstylename as iFRefOWrite.$curstylename
				Do iFRefOWrite.$undoenabled.$assign(kTrue)
			Else
				Do method $formatChanged     ;; this restores our style list
			End If
		Case kStyleActionRedefineStyle
			No/Yes message Redefine Style {Redefine the style "[iFRefOWrite.$curstylename]" using the formatting of the current selection?}
			If flag true
				;  first get all the values of the properties from the current selection
				While len(redefinePropsRead)
					Calculate curProp as strtok(nam(redefinePropsRead),',')
					Calculate redefineValues as con(redefineValues,iFRefOWrite.$[curProp],',')
				End While
				;  now assign the values to the current style
				Do iFRefOWrite.$startundo()
				Do iFRefOWrite.$editstylename.$assign(iFRefOWrite.$curstylename)
				While len(redefinePropsWrite)
					Calculate curProp as strtok(nam(redefinePropsWrite),',')
					Calculate iFRefOWrite.$[curProp] as strtok(nam(redefineValues),',')
				End While
				Do iFRefOWrite.$editstylename.$assign('')
				Do iFRefOWrite.$endundo("Redefine Style")
			End If
			Do method $formatChanged     ;; this restores our style list
		Case kStyleActionAddNewStyle
			While kTrue
				;  ask user to enter a new style name
				Prompt for input Please enter a name for the new style.[chr(13,13)]The new style will be created with the formatting [chr(13)]of the current paragraph./Create Style Returns newStyleName (Cancel button,Prompt above entry)
				If flag false
					Do method $formatChanged     ;; this restores our style list
					Quit event handler (Pass to next handler)
				End If
				;  check the style name is not in use
				Do iFmtStyleList.$search(upp($ref.C1)=upp(newStyleName),kTrue,kFalse,kFalse,kFalse) Returns line
				If line
					OK message Style Error {The name '[newStyleName]' is already in use. Please try another name.}
				Else
					;  we can create a new style by simply assigning the new name to $curstylename
					Do iFRefOWrite.$startundo()
					Calculate iFRefOWrite.$curstylename as newStyleName
					Do iFRefOWrite.$endundo("Create Style")
					Quit event handler (Pass to next handler)
				End If
			End While
	End Switch
	Quit event handler (Pass to next handler)