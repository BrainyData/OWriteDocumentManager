;  PURPOSE:     ;; main method for exporting style information
;  USAGE:     ;; called by ...$writedoc and ...$writeobjs_para
;  SUB-CLASSING:     ;; only sub-class this method if there is a need to write a prefix or suffix for the export of style information
;  IMPLEMENTS:     ;; iterates a list of document objects and calls the appropriate export methods based on object type

;  export the styles ---
If pStyles.$linecount
	
	;  --- start Omnis TEXT buffer     ;; we write entire style using same buffer
	Do method $cinst.$buffer_start Returns #F
	If flag true
		
		;  --- open a XML dict for a document style
		If pDocument
			Text: {[ivIndent]<dict>} (Linefeed)
			Do method Indent (kTrue)
		End If
		
		;  --- call superclass to export styles     ;; this will call the various $writestyle_xxx methods which we override
		Do $inherited.$writestyles(pStyles,pDocument) Returns #F
		If flag false
			Quit method kFalse
		End If
		
		;  --- close the XML dict
		If pDocument
			Do method Indent (kFalse)
			Text: {[ivIndent]</dict>} (Linefeed)
		End If
		
		;  --- close Omnis text buffer     ;; writes buffer to output
		Do method $cinst.$buffer_end Returns #F
	End If
	
	Quit method #F
	
Else
	
	Quit method kTrue
	
End If