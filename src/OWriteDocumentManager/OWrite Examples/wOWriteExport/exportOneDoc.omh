Calculate ivOWrite.$evallocal as kTrue     ;; we want our window instances to be called for all calculations
If cvEvaluateDuringExport     ;; CHANGE_2013_05_01-1
	;  this will only work for calculations not dependant on the main example window
	;  but the calculations may refer to variables of this export window
	Do ivOWriteObj.$evalcalcs.$assign(kTrue)
End If
Switch cvOption
	Case kDestOWriteBinary     ;; CHANGE_2013_11_27_1 (requires OWrite version 4)
		If cvOWriteBinarySaveAsList     ;; CHANGE_2015_11_03__1 begins
			Do ivOWrite.$savedata(lListData,kWriFmtDefault,pMakeDataPermanent,kWriSaveAsList,kTrue)
			Calculate lBinData as OWrite.$listtobin(lListData)
		Else
			Do ivOWrite.$savedata(lBinData,kWriFmtDefault,pMakeDataPermanent)
		End If     ;; CHANGE_2015_11_03__1 ends
		Do method writeOneFile (pFilePath,lBinData)
	Case kDestPlainText
		;  by default plain text is saved as 8bit ANSI or 8bit Mac Roman, or 32 bit unicode in the unicode version of Omnis
		;  for our export we save to a .txt file so if we are unicode Omnis we force OWrite to export non-unicode 8bit characters
		Do ivOWrite.$savedata(lBinData,kWriFmtText,pMakeDataPermanent,kWriSaveNonUnicode,kTrue)
		Do method writeOneFile (pFilePath,lBinData)
	Case kDestUnicodeText
		;  unicode text files expect UTF16 with leading BOM character, so we save to binary
		Do ivOWrite.$savedata(lBinData,kWriFmtText,pMakeDataPermanent,kWriTextFmtUTF16,kTrue)
		Do method writeOneFile (pFilePath,lBinData)
	Case kDestRTF
		;  OWrite saves RTF as 7bit single byte text so we save to a binary field so that Omnis unicode does not corrupt it
		Do ivOWrite.$savedata(lBinData,kWriFmtRTF,pMakeDataPermanent)
		Do method writeOneFile (pFilePath,lBinData)
	Case kDestHTML
		;  OWrite saves HTML as UTF8 characters in unicode Omnis so we save to a binary field so that Omnis does not corrupt it
		Do ivOWrite.$savedata(lBinData,kWriFmtHTML,pMakeDataPermanent,kWriHtmlBookmarks,"Heading 1,Heading 2,Heading 3,Property Name,Constant Name",kWriHtmlSizeAdjust,kFalse,kWriHtmlRawSupport,cvHtmlRawSupport,kWriHtmlOmnisCompatible,cvHtmlOmnisCompatible,kWriHtmlTitle,con(ivGroupName,': ',pDocName),kWriHtmlNoAutoSize,cvHtmlNoAutoSize,kWriHtmlNoMargins,cvHtmlNoMargins)     ;; CHANGE_V3 added kWriHtmlOmnisCompatible and kWriHtmlTitle flags
		Do method writeOneFile (pFilePath,lBinData)
	Case kDestCustom     ;; CHANGE_2013_11_27_1 (requires OWrite version 4)
		Do ivOWrite.$savedata(lListData,kWriFmtDefault,pMakeDataPermanent,kWriSaveAsList,kTrue)
		Do lOWriteExportCustom.$export_document(pFilePath,pDocName,lListData)
End Switch
Calculate ivOWrite.$evallocal as kFalse