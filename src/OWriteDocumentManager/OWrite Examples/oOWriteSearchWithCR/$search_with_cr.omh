;  extract the text prior to the CR character and set as our search string
Calculate tmp as pSearchString
Calculate $cinst.$text as strtok(nam(tmp),chr(13))
;  initialize our find object
Do pOWriteRef.$findinit($cinst.$owriteobj,kTrue) Returns continue     ;; kTrue means search entire document ignoring current selection

;  we now enter our search loop
While continue
	;  findt text matching search string prior to CR
	Do pOWriteRef.$findnext($cinst.$owriteobj) Returns continue
	If continue
		;  now extract plain text from document using current start selection
		;  plus the length of our entire search string
		Calculate pOWriteRef.$lastsel as pOWriteRef.$firstsel+len(pSearchString)
		Calculate savedModified as pOWriteRef.$modified     ;; preserve our modified flag as our $savedata call will alter it.
		Do pOWriteRef.$savedata(tmp,kWriFmtText,kTrue,kWriSaveSelection,kTrue)
		Calculate pOWriteRef.$modified as savedModified
		;  now compare the plain text with our full search string
		;  if we want to ignore case, we can use the Omnis upp() function
		If tmp=pSearchString
			Quit method kTrue
		End If
	End If
End While
;  didn't find anything
Quit method kFalse