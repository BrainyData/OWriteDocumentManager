On evClick     ;; Event Parameters - pRow( Itemreference )
	;  get reference to spell checker object instance
	;  the way we do this insures that it does not matter which library instanciates the object
	;  there should only ever be one object instance
	Set reference OSpellObj to $root.$libs.OSpell2.$objects.oSpellChecker.$insts.$first().$ref
	Set main file {fDocuments}
	;  fetch the first record
	Find first
	While flag true
		;  set the title for the spell checker window
		Calculate OSpellObj.$title as con('Document "',fDocuments.title,'"')
		;  check the data
		Do OSpellObj.$checkdata(fDocuments.text,canceled,kFalse) Returns changed
		If changed
			;  the data has changed
			;  make a temp copy of the data and update the record
			Calculate tmpData as fDocuments.text
			Prepare for edit
			Calculate fDocuments.text as tmpData
			Update files
		End If
		If canceled     ;; test if the user has canceled
			Calculate #F as kFalse
		Else     ;; fetch the next record
			Next
		End If
	End While
	;  make sure the spell checker window is closed
	;  NOTE: We only want to show the complete message if the user hasn't canceled
	Do OSpellObj.$endcheck(not(canceled))
	;  ...done